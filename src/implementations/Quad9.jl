"""
"""
struct Quad9 <: AbstractQuad
end

"""
"""
function ElementStencil(e::Quad9, degree::I, Itype::Type = Integer, Rtype::Type = Float64) where I <: Integer
  points = [
    -1.0  1.0 1.0 -1.0  0.0 1.0 0.0 -1.0 0.0
    -1.0 -1.0 1.0  1.0 -1.0 0.0 1.0  0.0 0.0
  ]
  vertex_points = [1, 2, 3, 4]
  face_points = [
    1 2 3 4
    5 6 7 8
    2 3 4 1
  ]
  interior_nodes = [9]
  return ElementStencil{Itype, Rtype, Quad9}(e, degree, points, vertex_points, face_points, interior_nodes)
end

function shape_function_values(::Quad9, ξ)
  N = @SVector [
    0.25 * (ξ[1]^2 - ξ[1]) * (ξ[2]^2 - ξ[2]),
    0.25 * (ξ[1]^2 + ξ[1]) * (ξ[2]^2 - ξ[2]),
    0.25 * (ξ[1]^2 + ξ[1]) * (ξ[2]^2 + ξ[2]),
    0.25 * (ξ[1]^2 - ξ[1]) * (ξ[2]^2 + ξ[2]),
    0.50 * (ξ[2]^2 - ξ[2]) * (1.0 - ξ[1]^2),
    0.50 * (ξ[1]^2 + ξ[1]) * (1.0 - ξ[2]^2), 
    0.50 * (ξ[2]^2 + ξ[2]) * (1.0 - ξ[1]^2), 
    0.50 * (ξ[1]^2 - ξ[1]) * (1.0 - ξ[2]^2), 
    (1.0 - ξ[1]^2) * (1.0 - ξ[2]^2)
  ]
end

function shape_function_gradients(::Quad9, ξ)
  ∇N_ξ = @SMatrix [
    0.25 * (ξ[2]^2 - ξ[2]) * (2. * ξ[1] - 1.)  0.25 * (ξ[1]^2 - ξ[1]) * (2. * ξ[2] - 1.);
    0.25 * (ξ[2]^2 - ξ[2]) * (2. * ξ[1] + 1.)  0.25 * (ξ[1]^2 + ξ[1]) * (2. * ξ[2] - 1.);
    0.25 * (ξ[2]^2 + ξ[2]) * (2. * ξ[1] + 1.)  0.25 * (ξ[1]^2 + ξ[1]) * (2. * ξ[2] + 1.);
    0.25 * (ξ[2]^2 + ξ[2]) * (2. * ξ[1] - 1.)  0.25 * (ξ[1]^2 - ξ[1]) * (2. * ξ[2] + 1.);
    0.50 * (ξ[2]^2 - ξ[2]) * (-2.0 * ξ[1])     0.50 * (2.0 * ξ[2] - 1.0) * (1.0 - ξ[1]^2);
    0.50 * (2.0 * ξ[1] + 1.0) * (1.0 - ξ[2]^2) 0.50 * (ξ[1]^2 + ξ[1]) * (-2.0 * ξ[2]);
    0.50 * (ξ[2]^2 + ξ[2]) * (-2.0 * ξ[1])     0.50 * (2.0 * ξ[2] + 1.0) * (1.0 - ξ[1]^2);
    0.50 * (2.0 * ξ[1] - 1.0) * (1.0 - ξ[2]^2) 0.50 * (ξ[1]^2 - ξ[1]) * (-2.0 * ξ[2]);
    (-2.0 * ξ[1]) * (1.0 - ξ[2]^2)             (1.0 - ξ[1]^2) * (-2.0 * ξ[2])
  ]
end

export Quad9
